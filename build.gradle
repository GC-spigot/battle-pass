buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

group 'io.github.battlepass'
version '3.7'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'}
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://gitlab.com/api/v4/projects/14122684/packages/maven' } // BedWars1058
    maven { url 'http://dl.bintray.com/tastybento/maven-repo' } // ASkyBlock
    maven { url 'http://repo.citizensnpcs.co/' } // Citizens
    maven { url 'https://repo.codemc.org/repository/maven-public/' } // Some CodeCo stuff
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' } // PlaceholderAPI
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'lib') // Add everything in the lib folder.
    compileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'me.clip:placeholderapi:2.10.8'
    compileOnly 'net.citizensnpcs:citizens:2.0.26-SNAPSHOT'
    compileOnly 'com.wasteofplastic:askyblock:3.0.9.4'
    compileOnly 'com.andrei1058.bedwars:bedwars-api:19.10'
    compileOnly 'com.github.Angeschossen:LandsAPI:4.5.2.0'
    compileOnly 'com.github.electro2560:ClueScrollsAPI:-SNAPSHOT'
    implementation 'com.github.hyfe-dev:simple-spigot:1.0.10.5'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

processResources {
    expand project.properties
}

shadowJar {
    StringBuilder fileNameBuilder = new StringBuilder(project.name.concat('-'))
    if (this.project.hasProperty("buildNo")) {
        project.version = buildNo
        fileNameBuilder.append(this.project.buildNo)
    } else {
        fileNameBuilder.append(this.version)
    }
    archiveFileName = fileNameBuilder.toString().concat(".jar")
    relocate 'me.hyfe.simplespigot', "io.github.battlepass.simple-spigot"
}