buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

group 'io.github.battlepass'
version '3.12.1'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.andrei1058.com/releases/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' } // Some CodeCo stuff
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' } // PlaceholderAPI

    maven { url 'https://gitlab.com/api/v4/projects/14122684/packages/maven' } // BedWars1058
    maven { url 'https://dl.bintray.com/tastybento/maven-repo' } // ASkyBlock
    maven { url 'https://repo.citizensnpcs.co/' } // Citizens
    maven { url 'https://mvn.lumine.io/repository/maven-public/' } // MythicMobs
    maven { url 'https://bitbucket.org/kangarko/libraries/raw/master' } // All mineacademy, e.g BossTM
}

dependencies {
    implementation 'org.bstats:bstats-bukkit:1.7'
    compileOnly fileTree(include: ['*.jar'], dir: 'lib') // Add everything in the lib folder.
    compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT' // Spigot nms api
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    implementation 'com.github.hyfe-dev:simple-spigot:1.0.11.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    // Quests
    compileOnly 'com.github.electro2560:ClueScrollsAPI:-SNAPSHOT'
    compileOnly 'net.citizensnpcs:citizens:2.0.26-SNAPSHOT'
    compileOnly 'com.wasteofplastic:askyblock:3.0.9.4'
    compileOnly 'com.andrei1058.bedwars:bedwars-api:20.7'
    compileOnly 'com.github.Angeschossen:LandsAPI:4.5.2.0'
    compileOnly 'io.lumine.xikage:MythicMobs:4.9.1'
    compileOnly 'org.mineacademy.plugin:Boss-API:3.8.0'
}

processResources {
    expand project.properties
}

shadowJar {
    archiveFileName = project.name + "-" + this.version + ".jar"
    relocate 'me.hyfe.simplespigot', "io.github.battlepass.simple-spigot"
    relocate 'org.bstats', "io.github.battlepass.bstats"
}